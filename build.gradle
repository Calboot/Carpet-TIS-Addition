plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'fabric-loom' version '1.6-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'ce1aeb2b'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.3.1' apply false
}

preprocess {
	def mc1181    = createNode('1.18.1' , 1_18_02, '')
	def mc1182p1  = createNode('1.18.2-pre1' , 1_18_02, '')
	def mc1182p2  = createNode('1.18.2-pre2' , 1_18_02, '')
	def mc1182p3  = createNode('1.18.2-pre3' , 1_18_02, '')
	def mc1182    = createNode('1.18.2' , 1_18_02, '')

	mc1182.link(mc1182p3, null)
	mc1182p3.link(mc1182p2, null)
	mc1182p2.link(mc1182p1, null)
	mc1182p1.link(mc1181, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
